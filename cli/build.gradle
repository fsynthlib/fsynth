apply plugin: 'kotlin-platform-jvm'
apply plugin: 'jacoco'

sourceSets {
    main.kotlin.srcDirs = [ "src/" ]
    test.kotlin.srcDirs = [ "tst/" ]
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    expectedBy project(":core")

    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'it.krzeminski.fsynth.ConsoleEntryPointKt'
    }
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // Excluding songs and instruments from code coverage.
                    // Rationale: testing songs would be like testing MIDI files - some pieces of data.
                    // Regarding instruments, they are defined as math operations on simpler waveforms and testing them
                    // would bring no benefit, however operations alone (addition, multiplication) are still tested.
                    'it/krzeminski/fsynth/songs/**',
                    'it/krzeminski/fsynth/instruments/**',
            ])
        })
    }
}

check.dependsOn(jacocoTestReport)
