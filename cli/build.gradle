apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'jacoco'

repositories {
    maven {
        url "https://dl.bintray.com/krzema1212/it.krzeminski"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.github.ajalt:clikt:1.7.0"
    compile(project(":core"))

    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit"
    testCompile "it.krzeminski:PlotAssert:0.1.0-beta"
}

mainClassName = 'it.krzeminski.fsynth.ConsoleEntryPointKt'
applicationName = 'fsynth'

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    // Excluding songs and instruments from code coverage.
                    // Rationale: testing songs would be like testing MIDI files - some pieces of data.
                    // Regarding instruments, they are defined as math operations on simpler waveforms and testing them
                    // would bring no benefit, however operations alone (addition, multiplication) are still tested.
                    'it/krzeminski/fsynth/songs/**',
                    'it/krzeminski/fsynth/instruments/**',
            ])
        })
    }
}

check.dependsOn(jacocoTestReport)
