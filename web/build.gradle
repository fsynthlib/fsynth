apply plugin: 'kotlin-platform-js'
apply plugin: 'org.jetbrains.kotlin.frontend'

sourceSets {
    main.kotlin.srcDirs = [ "src/" ]
    test.kotlin.srcDirs = [ "tst/" ]
}

repositories {
    jcenter()
    maven {
        url "https://kotlin.bintray.com/kotlin-js-wrappers"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'org.jetbrains:kotlin-react-dom:16.4.1-pre.34-kotlin-1.2.50'
    expectedBy project(":core")

    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.32"
    }
}

// A temporary workaround to skip running Karma tests (they don't work).
// TODO: fix it in scope of #10.
tasks.whenTaskAdded { task ->
    if (task.name == "karma-run-single") {
        task.enabled = false
    }
}

def webFilesDestination = "${buildDir}/web"

compileKotlin2Js {
    kotlinOptions {
        metaInfo = true
        outputFile = "$webFilesDestination/web.bundle.js"
        sourceMap = true
        moduleKind = 'commonjs'
        main = "call"
    }
}

kotlinFrontend {
    npm {
        dependency "react"
        dependency "react-dom"
    }

    webpackBundle {
        bundleName = "main"
    }
}

task copyIndexHtml(type: Copy) {
    from file("src/index.html")
    into file(webFilesDestination)
}

task copyJavascriptBundle(type: Copy) {
    from file("${buildDir}/bundle/main.bundle.js")
    into file(webFilesDestination)
}

assemble.dependsOn copyIndexHtml
assemble.dependsOn copyJavascriptBundle
