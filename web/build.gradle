apply plugin: 'kotlin-platform-js'
apply plugin: 'org.jetbrains.kotlin.frontend'
apply plugin: 'kotlin-dce-js'

repositories {
    jcenter()
    maven {
        url "https://kotlin.bintray.com/kotlin-js-wrappers"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains:kotlin-react:16.6.0-pre.65-kotlin-$kotlin_version"
    compile "org.jetbrains:kotlin-react-dom:16.6.0-pre.65-kotlin-$kotlin_version"
    expectedBy project(":core")

    testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.32"
    }
}

compileKotlin2Js {
    kotlinOptions {
        outputFile = "$project.buildDir.path/js/${project.name}.js"
        moduleKind = "commonjs"
    }
}

compileTestKotlin2Js {
    kotlinOptions {
        moduleKind = "commonjs"
    }
}

kotlinFrontend {
    npm {
        dependency "react"
        dependency "react-dom"
        dependency("@material-ui/core", "3.3.2")
        dependency("@material-ui/icons", "3.0.1")

        devDependency "karma-chrome-launcher"
        devDependency "puppeteer"
    }

    webpackBundle {
        bundleName = "main"
        mode = "production"
    }

    karma {
        // All properties regarding Karma should be changed in the custom config file referred below.
        customConfigFile = "karma.conf.js"
    }
}

def filesToDeployDestination = "${buildDir}/deploy-me"

task copyIndexHtml(type: Copy) {
    from file("src/main/resources/index.html")
    into file(filesToDeployDestination)
}

task copyJavascriptBundle(type: Copy) {
    from file("${buildDir}/bundle/main.bundle.js")
    into file(filesToDeployDestination)
}

assemble.dependsOn copyIndexHtml
assemble.dependsOn copyJavascriptBundle
