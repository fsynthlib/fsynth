plugins {
    id 'org.jetbrains.kotlin.js'
}

repositories {
    jcenter()
    maven {
        url "https://kotlin.bintray.com/kotlin-js-wrappers"
    }
    maven {
        url "https://dl.bintray.com/krzema1212/it.krzeminski"
    }
}

kotlin {
    target {
        useCommonJs()
        browser {
            webpackTask {
                runTask {
                }
            }
        }
    }

    sourceSets {
        main {
            dependencies {
                implementation("org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion")
                implementation("org.jetbrains:kotlin-react:16.6.0-pre.68-kotlin-1.3.20")
                implementation("org.jetbrains:kotlin-react-dom:16.6.0-pre.68-kotlin-1.3.20")
                implementation(project(":core"))
                implementation(npm('react', '16.6.0'))
                implementation(npm('react-dom', '16.6.0'))
                implementation(npm('@material-ui/core', '3.3.2'))
                implementation(npm('@material-ui/icons', '3.0.1'))
            }
        }
        test {
            dependencies {
                implementation("org.jetbrains.kotlin:kotlin-test-js")
                implementation("it.krzeminski:PlotAssert:0.1.0-beta")
            }
        }
    }
}

def filesToDeployDestination = "${buildDir}/deploy-me"

task copyIndexHtml(type: Copy) {
    from file("src/main/resources/index.html")
    into file(filesToDeployDestination)
}

task copyJavascriptBundle(type: Copy) {
    from file("${buildDir}/distributions/web.js")
    into file(filesToDeployDestination)
}

assemble.dependsOn copyIndexHtml
assemble.dependsOn copyJavascriptBundle
